plugins {
	id 'java'
	id 'fabric-loom' version '0.12-SNAPSHOT'
	id 'signing'
	id 'maven-publish'
	id 'org.jetbrains.kotlin.jvm' version '1.7.10'
}

repositories {
	mavenCentral()
	mavenLocal()
}

dependencies {
	minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
	mappings loom.officialMojangMappings()
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'fabric-loom'
	apply plugin: 'signing'
	apply plugin: 'maven-publish'
	apply plugin: 'org.jetbrains.kotlin.jvm'

	repositories {
		maven { url "https://maven.terraformersmc.com/" }
		maven { url "https://maven.forpleuvoir.com/snapshots" }
		maven { url "https://maven.forpleuvoir.com/releases" }
		mavenCentral()
		mavenLocal()
	}

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17

	group = rootProject.mod_group
	version = rootProject.mod_version

	dependencies {
		minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
		mappings loom.officialMojangMappings()
		modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
		modImplementation "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
		modImplementation "net.fabricmc:fabric-language-kotlin:${rootProject.fabric_kotlin_version}"
		modApi "com.terraformersmc:modmenu:${rootProject.modmenu_version}"
	}

	processResources {
		inputs.property "version", rootProject.mod_version
		filteringCharset "UTF-8"

		filesMatching("fabric.mod.json") {
			expand "version": rootProject.mod_version
		}
	}

	tasks.withType(JavaCompile).configureEach {
		it.options.release = 17
		it.options.encoding = "UTF-8"
	}

	java {
		withSourcesJar()
		toolchain.languageVersion.set(JavaLanguageVersion.of(17))
	}

	jar {
		from("LICENSE") {
			rename { "${it}_${project.archivesBaseName}" }
		}
	}

	publishing {
		//see â†“
		//https://reposilite.com/guide/deployment#gradle
		publishing {
			publications {
				mavenJava(MavenPublication) {
					from components.java
				}
			}
		}
		repositories {
			maven {
				name = "releases"
				url = "https://maven.forpleuvoir.com/releases"
				credentials(PasswordCredentials)
				authentication {
					basic(BasicAuthentication)
				}
			}
			maven {
				name = "snapshots"
				url = uri("https://maven.forpleuvoir.com/snapshots")
				credentials(PasswordCredentials)
				authentication {
					basic(BasicAuthentication)
				}
			}
		}
	}

}